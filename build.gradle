import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

plugins {
    id "org.jetbrains.kotlin.android" version "1.6.10" apply false
    id "com.android.library" version "7.3.0" apply false
    id "org.jetbrains.dokka" version "1.6.10" apply true
    id "com.vanniktech.maven.publish" version "0.22.0" apply false
    id "io.gitlab.arturbosch.detekt" version "1.21.0"
    id "nl.neotech.plugin.rootcoverage" version "1.6.0"
}

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(project.file("dist"))
}

rootCoverage {
    generateXml true
    generateHtml false
    excludes = ["**/internal/model/**"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task reportMerge(type: ReportMergeTask) {
    output = rootProject.layout.buildDirectory.file("reports/detekt/merge.sarif")
}

subprojects {
    afterEvaluate {
        plugins.withType(DetektPlugin) {
            tasks.withType(Detekt) { detektTask ->
                finalizedBy(reportMerge)

                reportMerge.configure { mergeTask ->
                    mergeTask.input.from(detektTask.sarifReportFile)
                }
            }
        }
    }
}
